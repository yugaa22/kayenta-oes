/*
 * Copyright 2017 Google, Inc.
 * Copyright 2017 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id "io.spinnaker.project" version "$spinnakerGradleVersion" apply false
  id 'idea'
}

allprojects {
  apply plugin: "io.spinnaker.project"
  apply plugin: 'io.spinnaker.project'
  repositories {
    mavenLocal()
    mavenCentral()
  }
}

subprojects { project ->
  group = "io.spinnaker.kayenta"

  if (name != "kayenta-bom") {
    apply plugin: "java-library"
    apply plugin: "groovy"

    def localVersions = [orcaVersion].find { it.endsWith('-SNAPSHOT') }
    if (localVersions) {
      logger.info("Enabling mavenLocal repo for $localVersions")
      repositories {
        mavenLocal()
        maven{
          url "https://nexus.opsmx.net/repository/maven-snapshots/"
        }
      }
    }

    test {
      testLogging {
        exceptionFormat = "full"
      }
      minHeapSize = "512m"
      maxHeapSize = "512m"
    }

    tasks.withType(JavaExec) {
      if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8191'
      }
    }

    configurations.all {
      exclude group: 'org.mortbay.jetty', module: 'servlet-api'
      exclude group: 'javax.servlet', module: 'servlet-api'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
      exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
  }

  project.extensions.findByName('license')?.with {
    exclude "**/*.json"
    exclude "**/*.md"
    exclude "**/*.sse"
    exclude "**/*.yml"
  }

  if (name != "kayenta-bom") {
    dependencies {
      constraints {
        api "org.apache.commons:commons-math3:3.6.1"
        api 'org.scala-lang:scala-library-all:2.12.12'
        api 'org.scala-lang:scala-reflect:2.12.12'
        api 'com.typesafe.scala-logging:scala-logging_2.12:3.9.2'
        testImplementation 'org.scalatest:scalatest_2.12:3.0.9'
      }
      implementation enforcedPlatform("io.spinnaker.orca:orca-bom:$orcaVersion")
      compileOnly "org.projectlombok:lombok"
      annotationProcessor platform("io.spinnaker.orca:orca-bom:$orcaVersion")
      annotationProcessor "org.projectlombok:lombok"
      annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
      testAnnotationProcessor platform("io.spinnaker.orca:orca-bom:$orcaVersion")
      testAnnotationProcessor "org.projectlombok:lombok"
      testCompileOnly "org.projectlombok:lombok"


      api "org.slf4j:slf4j-api"
      api "org.springframework.boot:spring-boot-starter-web"
      api "io.spinnaker.kork:kork-swagger"
      api "io.spinnaker.orca:orca-core"

      api "com.squareup.retrofit:retrofit:1.9.0"
      api "com.squareup.retrofit:converter-jackson:1.9.0"
      api "com.squareup.okhttp:okhttp:2.7.5"
      api "com.squareup.okhttp:okhttp-urlconnection:2.7.5"
      api "com.squareup.okhttp:okhttp-apache:2.7.5"
      api "org.jsoup:jsoup:1.15.3" //CVE-2022-36033:jinjava-2.6.0 > jsoup-1.14.2


      testImplementation "org.springframework.boot:spring-boot-starter-test"
      testImplementation "org.spockframework:spock-core"
      testImplementation "org.spockframework:spock-spring"
      testImplementation "org.springframework:spring-test"
      testImplementation "org.hamcrest:hamcrest-core"
      testImplementation "org.apache.groovy:groovy-all"
      testRuntimeOnly "cglib:cglib-nodep"
      testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
      testRuntimeOnly "org.objenesis:objenesis"
    }
  }

}

defaultTasks ":kayenta-web:run"
